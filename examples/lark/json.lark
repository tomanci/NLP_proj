// A value in a json file is a list, a dictionary
// or a string, number, or a predefined constant

value: dict
     | list
     | ESCAPED_STRING
     | SIGNED_NUMBER
     | "true" | "false" | "null"

// a list is a sequence of values between []
// Values are defined recursively by the previous
// rule, such that a value can be a list itself
// to create lists of lists ex. [[1,2,3],[3,4,5]]
// it may be empty and values are separated by ,
list : "[" [value ("," value)*] "]"

// a dictionary is a sequence of pairs between {}
// it may be empty and pairs are separated by ,
dict : "{" [pair ("," pair)*] "}"

// a pair is a key:value
// The value in the pair is defined recursively such that
// it can be another dictionary, a list, etc.
// ex. {"a":1, "b":[1,2,3], "c": {"d": true}}
pair : ESCAPED_STRING ":" value

// Import terminals from Lark library

// escaped strings are string constants between ""
// they may contain escape sequences as \n, \\, \"
%import common.ESCAPED_STRING
%import common.SIGNED_NUMBER

// Ignore whitespaces
%import common.WS
%ignore WS
